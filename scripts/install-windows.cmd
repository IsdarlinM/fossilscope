@echo off
setlocal EnableExtensions
set "CMD=fossilscope"
set "SCRIPT_DIR=%~dp0"
for %%I in ("%SCRIPT_DIR%..") do set "REPO_ROOT=%%~fI"
set "CONSTRAINTS=%REPO_ROOT%\requirements\runtime-py311.lock"
set "INSTALL_ROOT=%USERPROFILE%\.fossilscope"
set "VENV=%INSTALL_ROOT%\venv"
set "BIN_DIR=%USERPROFILE%\.local\bin"
where py >nul 2>&1
if not errorlevel 1 (set "PY_CMD=py -3.11") else (where python >nul 2>&1 || (echo Python 3.11+ is required.& exit /b 2)& set "PY_CMD=python")
if not exist "%INSTALL_ROOT%" mkdir "%INSTALL_ROOT%"
if not exist "%BIN_DIR%" mkdir "%BIN_DIR%"
if not exist "%VENV%\Scripts\python.exe" %PY_CMD% -m venv "%VENV%" || exit /b 1
"%VENV%\Scripts\python.exe" -m pip install --upgrade pip || exit /b 1
set "SRIC_SOURCE=%SRIC_CORE_SOURCE%"
if not defined SRIC_SOURCE if exist "%REPO_ROOT%\..\sric-core\pyproject.toml" set "SRIC_SOURCE=%REPO_ROOT%\..\sric-core"
if defined SRIC_SOURCE ("%VENV%\Scripts\python.exe" -m pip install --upgrade -c "%CONSTRAINTS%" "%SRIC_SOURCE%" || exit /b 3) else ("%VENV%\Scripts\python.exe" -m pip install -c "%CONSTRAINTS%" "sric-core>=0.4,<0.5" || (echo SRIC Core 0.4.x is required.& exit /b 3))
"%VENV%\Scripts\python.exe" -m pip install --upgrade -c "%CONSTRAINTS%" "%REPO_ROOT%" || exit /b 1
>"%BIN_DIR%\%CMD%.cmd" echo @"%VENV%\Scripts\%CMD%.exe" %%*
"%VENV%\Scripts\%CMD%.exe" doctor || exit /b 1
echo FossilScope installed successfully.
